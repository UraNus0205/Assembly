# 第一章 8086微处理器

## 1.1 8086微处理器的结构

### 1.1.1 执行部件和总线接口部件

微处理器执行一段程序通常是通过重复执行以下步骤完成的：

①从内存储器中取出一条指令，分析指令操作码；

②读出一个操作数（如果指令需要操作数）；

③执行指令；

④将结果写入内存储器或输出设备（如果指令需要）。

为了充分利用总线，8086微处理器被设计为两个独立的功能部件，即**总线接口部件和执行部件**。

1. 总线接口部件

   总线接口部件（BIU）由段寄存器、指令指针、地址形成逻辑、总线控制逻辑和指令队列等组成。

   BIU的功能：同**外部总线连接**，为EU和内存及外设接口之间提供信息通路，并且计算形成20位的内存储器的物理地址。

2. 执行部件

   执行部件（EU）由通用寄存器、标志寄存器、ALU和EU控制系统等组成。EU从BIU的指令队列中获得指令，然后执行该指令，完成指令所规定的计算。

EU和BIU这两个功能部件能相互独立地进行工作，并且大多数情况下，能使大部分的取指令和执行指令重叠进行。

PS：8086的指令队列是6字节长。





> <center>8086系统存储器组织的基本原则
> </center>
>1.8086有16根数据线，20根地址线，时钟频率为5MHz，最大寻址空间为$2^{20}=1MB$。
> 
>2.8086通过低8位数据总线访问偶地址存储单元或I/O接口
> 
>   8086通过高8位数据总线访问奇地址存储单元或I/O接口



# 第二章 汇编语言

## 2.1 寄存器

一个典型的CPU由运算器、控制器、寄存器等器件构成，这些器件靠内部总线相连。在CPU中：

- 运算器进行信息处理；
- 寄存器进行信息存储；
- 控制器控制各种器件进行工作；
- 内部总线连接各种器件，在他们之间进行数据的传送

### 2.1.1 通用寄存器

8086CPU所有的寄存器都是16位的，可以存放两个字节。AX、BX、CX、DX即为通用寄存器。

- AX可分为AH和AL；（AH代表AX高8位，AL代表AL低8位）
- BX可分为BH和BL；
- CX可分为CH和CL；
- DX可分为DH和DL；

### 2.1.2 字在寄存器中的存储

- 字节：byte，一个字节由8bit组成；
- 字：word，一个字由两个字节组成。

### 2.1.3 几条汇编指令

在进行数据传送或运算时，要注意指令的两个操作对象的位数应该一致，例如：

```assembly
MOV AX,BX
MOV BX,CX
MOV AX,18H
MOV AL,18H
ADD AX,BX
ADD AX,20000
```

等都是正确的指令，而：

```assembly
MOV AX,BL;寄存器位数不同
MOV BH,AX;
MOV AL,20000;8位寄存器最大可存放值为255的数据
ADD AL,100H;100H=256,产生溢出
```

### 2.1.4 8086CPU给出物理地址的方法

$物理地址=段地址×16（基础地址）+偏移地址$

CPU可以使用不同的段地址和偏移地址形成同一个物理地址。

比如CPU要访问21F60H单元，则它给出的段地址SA和偏移地址EA满足$SA×16+EA=21F60H$即可

### 2.1.5 段的概念

在编程时可以根据需要，将**若干地址连续的内存单元看作一个段（系统并未真的分段）**，用段地址×16定位段的起始地址（基础地址），用偏移地址定位段中的内存单元。**一个段的长度最大为64KB**。

### 2.1.6 段寄存器

- CS:代码段寄存器
- SS:堆栈段寄存器
- DS:数据段寄存器
- ES:附加段寄存器

### 2.1.7 CS和IP

CS为代码段寄存器，IP为指令指针寄存器。

在8086PC机中，任意时刻，设CS中的内容为M，IP中的内容为N，**8086CPU将从内存M×16+N单元开始，读取一条指令并执行。即CPU将CS:IP指向的内容当做指令执行。**如果说，内存中的一段信息曾被CPU执行过的话，那么它所在的内存单元必然被CS：IP指向过。

### 2.1.8 修改CS、IP的指令

在CPU中，程序员能够用指令读写的部件只有寄存器，但是**`MOV`指令不能用于设置CS和IP的值**。能够修改CS、IP的内容的指令被统称为转移指令，比如JMP。

若想同时修改CS、IP的内容，可以用形如`JMP段地址:偏移地址`的指令完成，如：

```assembly
JMP 2AE3:3;#执行后CS=2AE3H,IP=0003H,CPU将从2AE33H处读取命令
```

若想仅修改IP的内容，可用形如`JMP 某一合法寄存器`的指令完成，如：

```assembly
JMP AX;#指令执行前：AX=1000H,CS=2000H,IP=0003H
	   #指令执行后：AX=1000H,CS=2000H,IP=1000H
	   #类似MOV IP,AX;
```

### 2.1.9 代码段

将一段内存当做代码段，仅仅是我们在编程时的一种安排，CPU并不会由于这样的安排，就自动地将我们定义的代码段中的指令当做指令来执行。**CPU只认被CS:IP指向的内存单元中的内容为指令。**所以，要让CPU执行我们放在代码段中的指令，**必须要将CS:IP指向所定义的代码段中的第一条指令的首地址。**若我们将一段代码存放在123B0H~123B9H内存单元中，将其定义为代码段，如果要让代码得到执行，可设CS=123BH,IP=0000H. 

### 2.1.10 内存中字的存储

存储器是以8位(一个字节)为一个存储单元编址的。并用唯一的一个地址码来表示。任何连续存放的两个字节都可以称为一个字。**存放时低位字节存入低地址，高位字节存入高地址。处于低地址的字节的地址为这个字的地址。**

偶数地址开始的字称为规则字，奇数地址开始的字称为非规则字。

### 2.1.11 DS和[address]

8086CPU中有一个**DS寄存器，通常用来存放要访问数据的段地址。**比如我们要读取10000H单元的内容，可以用如下的程序段进行。

```assembly
MOV BX,1000H
MOV DS,BX;8086CPU不支持将数据直接送入段寄存器的操作，只能间接送入。CS是无论如何都不支持。
MOV AL,[0]
```

上面三条指令将1000:0中的数据读到AL中。

`[...]`表示一个内存单元，`[0]`中的0表示内存单元的偏移地址。指令执行时，8086CPU自动取**DS中的数据作为内存单元的段地址。**

### 2.1.12 MOV、ADD、SUB指令

1. 双操作数指令不允许两个操作数同时为段寄存器或存储器操作数（串操作除外）。
2. 代码段寄存器**CS和立即数均不能作为目的操作数，立即数也不能传送到段寄存器中。**
3. 目的操作数与源操作数必须类型匹配，即同时是字节或字类型。**至少有一个操作数的类型要明确。**

```assembly
MOV    AX,BL;类型不匹配
MOV    DS,1000H;不允许立即数送段寄存器       
MOV    [BX],[SI];不允许内存操作数之间传送
MOV    ES,CS;不允许段寄存器之间传送            
MOV    CS,AX;CS不能作为目的操作数
MOV    AX,CS;√         
```

### 2.1.13 CPU提供的栈机制

8086CPU提供入栈和出栈指令，最基本的两个是PUSH（入栈）和POP（出栈）。比如`PUSH AX`表示将寄存器AX中的数据送入栈中。**8086CPU的入栈和出栈操作都是以字为单位进行的。**

8086CPU中，有两个寄存器，段寄存器SS和寄存器SP，栈顶的段地址存放在SS中，偏移地址存放在SP中。**任意时刻，SS:SP指向栈顶元素。**`PUSH`指令和`POP`指令执行时，CPU从SS和SP中得到栈顶的地址。

`PUSH AX`的执行，由以下两步完成。

1. `SP=SP-2`SS:SP指向当前栈顶前面的单元，以当前栈顶前面的单元为新的栈顶；
2. 将AX中的内容送入SS:SP指向的内存单元处，SS:SP此时指向新栈顶。

当栈为空的时候，栈中没有元素，也就不存在栈顶元素，所以SS:SP只能指向栈的**最底部单元下面的单元**。比如若将10000H~1000FH这段空间作为栈，栈空时，SP=0010H。

`POP AX`的执行，由以下两步完成。

1. SS:SP指向的内存单元处的数据送入AX中；
2. `SP=SP+2`SS:SP指向当前栈顶下面的单元，以当前栈顶下 面的单元为新的栈顶；

### 2.1.14 栈顶超界的问题

我们在编程的时候只能自己小心栈顶超界的问题，要根据可能用到的最大栈空间，来安排栈的大小，防止入栈的数据太多而导致的超界；执行出栈操作的时候也要注意，以防栈空的时候继续出栈而导致的超界。

### 2.1.15 PUSH、POP指令

```assembly
MOV AX,1000H;
MOV DS,AX;内存单元的段地址要放在DS中
PUSH [0];将1000:0处的字压入栈中
POP [2];出栈，出栈的数据送入1000:2处
```

指令执行时，CPU要知道内存单元的地址，可以在`PUSH、POP`指令中只给出内存单元的偏移地址，**段地址在指令执行时，CPU从DS中取得。**

## 2.2 [BX]和loop指令

1. [BX]和内存单元的描述

   [0]表示内存单元，偏移地址为0.

   [BX]同样也表示一个内存单元，它的偏移地址在BX中。

2. 我们定义的描述性的符号：“()”

   为了描述上的简洁，我们将使用一个描述性的符号“()”来表示一个寄存器或一个内存单元中的内容。

   比如：(AX)表示AX中的内容，(20000H)表示内存20000H单元的内容（()中的内存单元的地址是物理地址）。

   (2000:0)、((DS):1000H)是不正确的用法。

3. 约定符号idata表示常量

### 2.2.1 [BX]

```assembly
MOV AX,[BX];(AX)=((DS)*16+(BX))
```

### 2.2.2 Loop指令

Loop指令的格式是：**Loop 标号**，CPU执行Loop操作时，要进行两步操作：

1. **(CX)=(CX)-1;**
2. 判断CX中的值，不为零则转至标号处执行程序，如果为零则向下执行。

通常，我们用Loop指令来实现循环功能，CX中存放循环次数。

例如编程计算$2^{12}$

```assembly
assume cs:code
code segment
   mov ax,2
   mov cx,11
s: add ax,ax
   loop s
   mov ax,4c00h
   int 21h
code ends
end
```

### 2.2.3 Debug和汇编编译器masm对指令的不同处理

1. 在汇编源程序中，若用指令访问一个内存单元，则在指令中必须用“[...]”来表示内存单元，如果在"[]"里用一个常量idata直接给出内存单元的偏移地址，就要在“[]”的前面显式地给出段地址所在的寄存器。比如：

   ```assembly 
   MOV AL,DS:[0]
   MOV AL,[0];这里会把[idata]解释为idata,即MOV AL,0
   ```

### 2.2.4 Loop和[BX]的联合应用

考虑这样一个问题，计算FFFF:0~FFFF:B单元中的数据的和，结果存储在DX中。先分析一下：

1. 运算后的结果是否会超出DX的范围？

   不会，FFFF:0~FFFF:B中都是字节型数据，最大255,12个这样的数据不会大于65535。

2. 我们能否将FFFF:0~FFFF:B中的数据直接累加到DX中？

   不可以，FFFF:0~FFFF:B中的数据是8位的，不能直接加到16位寄存器中。

3. 我们能否将FFFF:0~FFFF:B中的数据累加到DL中，并设置(DH)=0，从而实现累加到DX中？

   不可以，DL是8位寄存器，最多能容纳数据大小为255，可能会超界。

4. 我们到底怎样将FFFF:0~FFFF:B中的8位数据，累加到16位寄存器DX中？

   目前的方法（后续会有其他方法）是得用一个16位寄存器来做中介，将8位数据赋值到一个16位寄存器AX中，再将AX中的数据加到DX上。

```assembly
assume cs:code
code segment
	mov ax,0ffffh
	mov ds,ax
	
	mov bx,0
	mov dx,0
	mov cx,12
s:  mov al,[bx]
    mov ah,0
    add dx,ax
    inc bx
    loop s
    
    mov ax,4c00h
    int 21h
code ends
end
```

### 2.2.5 段前缀

指令`MOV AX,[BX]`中，内存单元的偏移地址由BX给出，段地址默认在DS中。我们可以在访问内存单元的指令中显示地给出内存单元的段地址所在的寄存器，比如：`MOV AX,DS:[BX]`

### 2.2.6 段前缀的使用

考虑会这样一个问题，将内存FFFF:0~FFFF:B单元中的数据复制到0:200:0:20B（等同于0200:0-0200:B）单元中。

因源单元FFFF:X和目标单元0200:X相距大于64KB，在不同的64KB段里，应该使用两个段。

```assembly
assume cs:code
code segment
    mov ax,0ffffh
    mov ds,ax
    
    mov ax,0200h
    mov es,ax
    
    mov bx,0
    mov cx,12
    
s:  mov dl,ds:[bx]
    mov es:[bx],al
    
    inc bx
    loop s
    
    mov ax,4c00h
    int 21h         
code ends
end
```

## 2.3 包含多个段的程序

### 2.3.1 在代码段中使用数据

考虑这样一个问题，编程计算以下8个数据的和，结果存在AX寄存器中：

0123H、0456H、0789H、0ABCH、0DEFH、0FEDH、0CBAH、0987H

前面的例子都是累加某些数据单元中的数据，并不关心数据本身。可现在要累加的就是已经给定了数值的数据。我们可以将它们一个一个地加到AX寄存器中，但是我们希望可以用循环的方式进行累加，所以累加前要将这些数据存储在一组地址连续的内存单元中。

从规范的角度来讲，我们是不能自己随便决定哪段空间可以使用的，应该让系统来为我们分配，我们可以在程序中，定义我们希望处理的数据，这些数据就会被编译、连接程序作为程序的一部分写到可执行文件中。当可执行文件中的程序被加载入内存时，这些数据也同时被加载入内存。与此同时，我们要处理的数据也就自然而然地获得了存储空间。

```assembly
assume cs:code
code segment
    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah
    start:mov bx,0
          mov ax,0
          
          mov cx,8
        s:add ax,cs:[bx]
          add bx,2
          loop s
          
          mov ax,4c00h
          int 21h
    
code ends 
end start
```

由于这8个数据在代码段中，程序在运行的时候CS中存放代码段的段地址，所以可以从CS中得到他们的段地址。因为用dw定义的数据处于代码段的最开始，所以偏移地址为0，这8个数据就在代码段的偏移0、2、4、6、8、A、C、E处。

**end除了通知编译器程序结束外，还可以通知编译器程序的入口在什么地方**，上述例子指明了程序的入口在标号start处，即`mov bx,0`是程序的第一条指令。

有了这种方法，就可以这样来安排程序的框架：

```assembly
assume cs:code
code segment
    		.
    		.
    	   数据
    		.
    		.
    start:  .
    		.
    		.
    	   代码
    		.
    		.
    		.
    
code ends 
end start
```

### 2.3.2 在代码段中使用栈

利用栈，将程序中定义的数据逆序存放

```assembly
assume cs:code
code segment
    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h  
    dw 0,0,0,0,0,0,0,0
    ;用dw定义8个字型数据，在程序加载后，将取得8个字的内存空间，存放这8个数据。
    ;在后面的程序中将这段空间当作栈来使用
    start:mov ax,cs
          mov ss,ax
          mov sp,20h;将设置栈顶ss:sp指向cs:20
          
          mov bx,0
          mov cx,8
          
       s0:push cs:[bx]
          add bx,2
          loop s0
          
          mov bx,0
          mov cx,8
          
       s1:pop cs:[bx]
          add bx,2
          loop s1
    
code ends 
end start
```

### 2.3.3 将数据、代码、栈放入不同的栈

为了防止程序显得混乱以及数据过多时容易超界，应该考虑用多个段来存放数据、代码和栈。

```assembly
assume cs:code,ds:data,ss:stack

data segment  
    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
data ends
stack segment
    dw 0,0,0,0,0,0,0,0
    ;用dw定义8个字型数据，在程序加载后，将取得8个字的内存空间，存放这8个数据。
    ;在后面的程序中将这段空间当作栈来使用
stack ends
code segment
    start:mov ax,stack
          mov ss,ax
          mov sp,10h;将设置栈顶ss:sp指向cs:10
          
          mov ax,data
          mov ds,ax;ds指向data段
          
          mov bx,0
          mov cx,8
          
       s0:push [bx]
          add bx,2
          loop s0
          
          mov bx,0
          mov cx,8
          
       s1:pop [bx]
          add bx,2
          loop s1
    
code ends 
end start
```

### 2.3.4 编写、调试具有多个段的程序

1. 将下面的程序编译、连接，用Debug加载、跟踪，然后回答问题。

   ```assembly
   assume cs:code,ds:data,ss:stack
   
   data segment  
       dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
   data ends
   stack segment
       dw 0,0,0,0,0,0,0,0
   stack ends
   code segment
       start:mov ax,stack
             mov ss,ax
             mov sp,10h
             
             mov ax,data
             mov ds,ax
             
             push ds:[0]
             push ds:[2]
             pop ds:[2]
             pop ds:[0]
             
             mov ax,4c00h
             int 21
   code ends 
   end start
   ```

   - CPU执行程序，程序返回前，data段中的数据为多少？

     0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h

   - CPU执行程序，程序返回前，cs=<u>0712H</u>、ss=<u>0711H</u>、ds=<u>0710H</u>

   - 设程序加载后，code段的段地址为X，则data段的段地址为<u>X-2</u>，stack段的段地址为<u>X-1</u>。(因为是先申请data段，再申请stack段，最后申请code段)

2. 将下面的程序编译、连接，用Debug加载、跟踪，然后回答问题。

   ```assembly
   assume cs:code,ds:data,ss:stack
   
   data segment  
       dw 0123h,0456h
   data ends
   stack segment
       dw 0,0
   stack ends
   code segment
       start:mov ax,stack
             mov ss,ax
             mov sp,10h
             
             mov ax,data
             mov ds,ax
             
             push ds:[0]
             push ds:[2]
             pop ds:[2]
             pop ds:[0]
             
             mov ax,4c00h
             int 21
   code ends 
   end start
   ```

   

   - CPU执行程序，程序返回前，data段中的数据为多少？

     0123h,0456h,其余为00补全	

   - CPU执行程序，程序返回前，cs=<u>0712H</u>、ss=<u>0711H</u>、ds=<u>0710H</u>

   - 设程序加载后，code段的段地址为X，则data段的段地址为<u>X-2</u>，stack段的段地址为<u>X-1</u>。(因为是先申请data段，再申请stack段，最后申请code段)

   - 对于如下定义的段：

     ```assembly
     name segment
     .
     .
     .
     name ends
     ```

     如果段中的数据占N个字节，则程序加载后，该段实际占有的空间为<u>（N/16的取整数+1）*16个字节</u>。

3. 将下面的程序编译、连接，用Debug加载、跟踪，然后回答问题。

   ```assembly
   assume cs:code,ds:data,ss:stack
   
   
   code segment
       start:mov ax,stack
             mov ss,ax
             mov sp,10h
             
             mov ax,data
             mov ds,ax
             
             push ds:[0]
             push ds:[2]
             pop ds:[2]
             pop ds:[0]
             
             mov ax,4c00h
             int 21
   code ends      
   
   data segment  
       dw 0123h,0456h
   data ends
   stack segment
       dw 0,0
   stack ends
   end start
   ```

   - CPU执行程序，程序返回前，data段中的数据为多少？

     0123h,0456h,其余为00补全	

   - CPU执行程序，程序返回前，cs=<u>0710H</u>、ss=<u>0714H</u>、ds=<u>0713H</u>

   - 设程序加载后，code段的段地址为X，则data段的段地址为<u>X+3，stack段的段地址为<u>X+4/u>。

     (**为什么是这样？怎么计算的？看cx，**程序加载时，我们发现cx=0044，含义：此程序所有机器码占用的空间是44H=68字节（cx 指示 程序机器码占用空间的大小），data 和 stack 由于定义的都是小于16个字节，一律按照16个字节分配空间，其余补00；剩余的36个字节就是code段真正的可执行的机器码。由于code段不足48个字节（3\*16），故程序加载时也补0了)

4. 如果将1,2,3中题中的最后一条伪指令“end start”改为“end”（即不指明程序的入口），则哪个程序可以正确执行？

   都可以正常运行，只有3可以正确运行，因为若没有end指明程序入口，程序会从最开始进行执行。

5. 程序如下，编写code段中的代码，将a段和b段中的数据依次相加，将结果存放到c段中。

   ```assembly
   assume cs:code
   a segment
       db 1,2,3,4,5,6,7,8
   a ends
    
   b segment
       db 1,2,3,4,5,6,7,8
   b ends
    
   c segment
       db 0,0,0,0,0,0,0,0
   c ends
    
   code segment
   start:
       mov ax,a
       mov es,ax
       
       mov ax,c
       mov ds,ax
       
       mov bx,0
       mov cx,8
   s1:
       mov ax,es:[bx]
       add [bx],ax
       add bx,2
       loop s1
       mov ax,b 
       mov es,ax
       mov ds,ax 
       mov bx,0
       mov cx,8
   s2:
       mov ax,es:[bx]
       add [bx],ax
       add bx,2
       loop s2
       
       mov ax,4c00h
       int 21h
   code ends
   end start
   ```

6. 程序如下，编写code段中的代码，用push指令将a段中的前8个字型数据逆序存储到b段中。

   ```assembly
   assume cs:code
    
   a segment
       dw 1,2,3,4,5,6,7,8,9,0ah,0bh,0ch,0dh,0eh,0fh,0ffh
   a ends
    
   b segment
       dw 0,0,0,0,0,0,0,0
   b ends
    
   code segment
   start:
       mov ax,a
       mov ds,ax        ;ds指向a段
    
       mov ax,b
       mov ss,ax        ;ss指向了b段
       mov sp,16        ;初始化栈顶，ss：sp指向了栈顶，意味着b段是个栈结构了。
    
       mov bx,0
       mov cx,8         ;循环读取a段8次，因为是前8个字
   s:
       push ds:[bx] ;直接将a段中的字单元内存压栈即可。这样在栈中的存储结构就是逆序的
       add bx,2
       loop s
       
       mov ax,4c00h
       int 21h
   code ends
   end start
   ```

   

